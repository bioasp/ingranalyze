input(P,V) :- input(V), exp(P).
exp(P) :- obs_vlabel(P,V,S).
exp("empty") :- 0[obs_vlabel(P,V,S)]0.

% The following code constitutes the core part of consistency checking.

1{vlabel(P,V,1), vlabel(P,V,-1)}1 :- exp(P), vertex(V), not input(P,V).
1{vlabel(P,U,1), vlabel(P,U,-1)}1 :- exp(P), edge(U,V), not input(P,V).
1{elabel(U,V,1), elabel(U,V,-1)}1 :- exp(P), edge(U,V), not input(P,V).

vlabel(P,V,S) :- vertex(V), obs_vlabel(P,V,S).
error_edge(U,V) :- obs_elabel(U,V,1), obs_elabel(U,V,-1).
elabel(U,V,S) :- edge(U,V), obs_elabel(U,V,S), not error_edge(U,V).

infl(P,V,S*T) :- elabel(U,V,S), vlabel(P,U,T).

:- vlabel(P,gen(V),S),  not infl(P,gen(V),S),  not input(P,gen(V)).
:- vlabel(P,and(V),1),      infl(P,and(V),-1), not input(P,and(V)).
:- vlabel(P,and(V),-1), not infl(P,and(V),-1), not input(P,and(V)).
:- vlabel(P,or(V),1),   not infl(P,or(V),1),   not input(P,or(V)).
:- vlabel(P,or(V),-1),      infl(P,or(V),1),   not input(P,or(V)).

infl(P,strong_inhibitor(V1,V2),V1,S*T) :- elabel(V1,strong_inhibitor(V1,V2),S), vlabel(P,V1,T).
infl(P,strong_inhibitor(V1,V2),V2,S*T) :- elabel(V2,strong_inhibitor(V1,V2),S), vlabel(P,V2,T).

:- vlabel(P,strong_inhibitor(V1,V2),1), infl(P,strong_inhibitor(V1,V2),V2,1),   not input(P,strong_inhibitor(V1,V2)).
:- vlabel(P,strong_inhibitor(V1,V2),1), not infl(P,strong_inhibitor(V1,V2),V1,1),   not input(P,strong_inhibitor(V1,V2)).
vlabel(P,strong_inhibitor(V1,V2),1):- infl(P,strong_inhibitor(V1,V2),V1,1), infl(P,strong_inhibitor(V1,V2),V2,-1), vertex(strong_inhibitor(V1,V2)), exp(P).

infl(P,opposite_sign(V),V,S*T) :- elabel(V2,opposite_sign(V),S), vlabel(P,V,T).

vlabel(P,opposite_sign(V),-1) :- infl(P,opposite_sign(V),V,1), vertex(opposite_sign(V)), exp(P).
vlabel(P,opposite_sign(V),1) :- infl(P,opposite_sign(V),V,-1), vertex(opposite_sign(V)), exp(P).


#hide.
%#show infl/3.
%#show elabel/3.
%#show vlabel/3.
